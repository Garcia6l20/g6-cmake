if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU" OR CMAKE_CXX_COMPILER_ID MATCHES ".*Clang")
  set(SANITIZERS "")
  list(APPEND SANITIZERS address leak undefined thread)
elseif(MSVC)
  list(APPEND SANITIZERS address)
endif()

if(SANITIZERS)
    foreach(SANITIZER ${SANITIZERS})
        add_library(g6_sanitizer_${SANITIZER} INTERFACE)
        add_library(g6::sanitizer::${SANITIZER} ALIAS g6_sanitizer_${SANITIZER})

        if(NOT MSVC)
          target_compile_options(g6_sanitizer_${SANITIZER} INTERFACE -fsanitize=${SANITIZER} -fno-omit-frame-pointer)
          target_link_options(g6_sanitizer_${SANITIZER} INTERFACE -fsanitize=${SANITIZER} -fno-omit-frame-pointer)
        else()
          target_compile_options(g6_sanitizer_${SANITIZER} INTERFACE /fsanitize=${SANITIZER} /Zi /INCREMENTAL:NO)
          target_link_options(g6_sanitizer_${SANITIZER} INTERFACE /INCREMENTAL:NO)
        endif()
    endforeach()
    # add none sanitizer (empty target used for tests without sanitizer)
    add_library(g6_sanitizer_none INTERFACE)
    add_library(g6::sanitizer::none ALIAS g6_sanitizer_none)
endif()
set(G6_AVAILABLE_SANITIZERS "none;${SANITIZERS}" CACHE INTERNAL "Available sanitizers" FORCE)
